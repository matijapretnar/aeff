type callId = int
type loc = int
type val = int

operation lookupReq : loc * callId
operation updateReq : loc * val * callId
operation allocReq : val * callId

operation lookupRes : val * callId
operation updateRes : callId
operation allocRes : loc * callId

let empty = []

let rec lookupHeap ((l', v) :: heap') l =
  if l = l' then v else lookupHeap heap' l

let rec updateHeap ((l', v') :: heap') l v =
  if l = l' then (l, v) :: heap' else (l', v') :: updateHeap heap' l v

let allocHeap heap v =
  let l = length heap in
  (l, v) :: heap, l

let heapRunner () =
    let heap = ref empty in
    let awaitLookup () =
        promise (lookupReq (l, callId) r ->
            let v = lookupHeap !heap l in
            send lookupRes (v, callId);
            r ()
        )
    in
    let awaitUpdate () =
        promise (updateReq (l, v, callId) r ->
            let heap' = updateHeap !heap l v in
            send updateRes callId;
            heap := heap';
            r ()
        )
    in
    let awaitAlloc () =
        promise (allocReq (v, callId) r ->
            let heap', l = allocHeap !heap v in
            send allocRes (l, callId);
            heap := heap';
            r ()
        )
    in
    awaitLookup ();
    awaitUpdate ();
    awaitAlloc ()

let lookup callCounter l =
    let callNo = !callCounter in
    send lookupReq (l, callNo);
    callCounter := callNo + 1;
    let p = promise (lookupRes (v, callNo') when callNo = callNo' ->
        return <<v>>
    ) in await p

let update callCounter l v =
    let callNo = !callCounter in
    send updateReq (l, v, callNo);
    callCounter := callNo + 1;
    let p = promise (updateRes (callNo') when callNo = callNo' ->
        return <<()>>
    ) in await p

let alloc callCounter v =
    let callNo = !callCounter in
    send allocReq (v, callNo);
    callCounter := callNo + 1;
    let p = promise (allocRes (l, callNo') when callNo = callNo' ->
        return <<l>>
    ) in await p

run
    let callCounter = ref 0 in
    let l = alloc callCounter 0 in
    let l' = alloc callCounter 10 in
    update callCounter l 10;
    update callCounter l' (lookup callCounter l + 4);
    return (lookup callCounter l, lookup callCounter l')

run
    heapRunner ()
